#version 450

layout (binding = 0, rgba32f) uniform image2D destTex;
layout (local_size_x = 16, local_size_y = 16) in;

uniform vec4 imageSize;
uniform float roughness_right;

// Constants
#define PI 3.14159265359

// Materials
#define LAMBERTIAN_MAT 0
#define REFLECTIVE_MAT 1
#define REFRACTIVE_MAT 2



#define VERTICAL_FIELD_OF_VIEW 90.0
#define NUM_SPHERES 4
#define CAST_DEPTH 64
#define MAX_ITERATIONS_PER_PIXEL_DIM 16
#define APERTURE 2.0

#define LOOK_FROM vec3(-2,2,1)
#define LOOK_AT vec3(0,0,-1)
#define VECTOR_UP vec3(0,1,0)




struct Ray
{
    vec3 orig;
    vec3 dir;
} ;//ray;

struct hit_info {
    vec3 p;
    vec3 normal;
    vec3 outward_normal;
    float t;
    bool front_face;
    int material;
    vec4 albedo;
    float roughness;
    float refr_index;
};

struct Sphere
{
    vec4 geom;
    int material;
    vec4 albedo;
    float roughness;
    float refr_index;
};

const Sphere spheres[NUM_SPHERES] = {
       Sphere(vec4(0.0, 0.0, -1.5, 0.5), LAMBERTIAN_MAT, vec4(0.7, 0.3, 0.3, 1.0), 0.0, 1.5),
       Sphere(vec4(-1.0, 0.0, -1.5, 0.5), REFRACTIVE_MAT, vec4(1.0, 1.0, 1.0, 1.0), 0.0, 1.5),
       Sphere(vec4(1.0, 0.0, -1.5, 0.5), REFLECTIVE_MAT, vec4(0.8, 0.6, 0.2, 1.0), roughness_right, 1.5),
       Sphere(vec4(0.0, -100.5, -1.5, 100.0), LAMBERTIAN_MAT, vec4(0.8, 0.8, 0.5, 1.0), 0.0, 1.5)
    };

const float infinity = 1.0/0.0;         // OpenGL > 4.1

uint seed = 123456;


// Utility Functions
float degrees_to_radians(float degrees);
bool isInRange(float t, float t_max, float t_min);

// Random
uint hash( uint x );
uint hash( uvec2 v );
uint hash( uvec3 v );
uint hash( uvec4 v );

float floatConstruct( uint m );

float random( uint x );
float random( float x );
float random( vec2  v );
float random( vec3  v );
float random( vec4  v );

vec3 random_on_unit_sphere();
vec3 random_on_unit_sphere(vec2 vec_seed);
vec3 random_on_unit_sphere(vec3 vec_seed);
vec3 random_in_unit_sphere(vec3 vec_seed);

// Traicing functions
vec4 ray_color(Ray r, int depth);
vec3 ray_point(Ray r, float t);
Ray get_ray(vec2 uv);

void material_scatter(hit_info hit, inout Ray scatter_ray);
void lambertian_scatter(hit_info hit, out Ray scatter_ray);
void reflective_scatter(hit_info hit, inout Ray scatter_ray);
void refractive_scatter(hit_info hit, inout Ray scatter_ray);

float fresnel_schlick_approximation(float cosine, float ri_ratio);

bool hit_sphere(Sphere s, Ray r, out hit_info hit, float t_min = 0.001, float t_max = 10000.0);
bool hit_world(Sphere s[NUM_SPHERES], Ray r, out hit_info hit, float t_min = 0.0, float t_max = 10000.0);

void main()
{
    // Image space calculation
    vec2 screenCoords = gl_GlobalInvocationID.xy;
    screenCoords.y = imageSize.y - gl_GlobalInvocationID.y;
    vec2 uv = vec2(screenCoords) / imageSize.xy;
    
    // Basic variables

    vec4 color = vec4(0.0);
    Ray ray;

    for(int i = 0; i < MAX_ITERATIONS_PER_PIXEL_DIM; ++i)
    {    
        for(int j = 0; j < MAX_ITERATIONS_PER_PIXEL_DIM; ++j)
        { 
            vec2 ray_uv = uv + vec2(float(i)/MAX_ITERATIONS_PER_PIXEL_DIM, float(j)/MAX_ITERATIONS_PER_PIXEL_DIM)/ imageSize.xy; 
            ray = get_ray(ray_uv);
            color += ray_color(ray, CAST_DEPTH);
        }
    }
    color /= MAX_ITERATIONS_PER_PIXEL_DIM * MAX_ITERATIONS_PER_PIXEL_DIM;

    color = sqrt(color);
    
    ivec2 storePos = ivec2(gl_GlobalInvocationID.xy);
    imageStore(destTex, storePos, color);
}

vec4 ray_color(Ray r, int depth)
{
    hit_info hit;

    vec4 color = vec4(1.0, 1.0, 1.0, 1.0);

    for(int i = 0; i < depth; ++i)
    {
        if(hit_world(spheres, r,  hit, 0.001, 100000000000.0)) 
        {
            material_scatter(hit, r);
            color.rgb *=  hit.albedo.rgb;
        }
        else
        {
            vec3 runitdir = normalize(r.dir);
            float t = 0.5*(runitdir.y + 1.0); 
    
            vec4 bckgrnd_color = mix( vec4(1.0, 1.0, 1.0, 1.0), vec4(0.5, 0.7, 1.0, 1.0), t);
            return color * bckgrnd_color;
        }
     }
     return color;
}

bool hit_world(Sphere s[NUM_SPHERES], Ray r, out hit_info hit, float t_min = 0.001, float t_max = 10000000000.0)
{
    bool hit_anything = false;
    float closest_to_camera = t_max;

    for(int i = 0; i < NUM_SPHERES; ++i)
    {
        if(hit_sphere(s[i], r, hit, t_min, closest_to_camera))
        {
            hit_anything = true;
            closest_to_camera = hit.t;
        }
    }

    return hit_anything;
}


bool hit_sphere(Sphere s, Ray r, out hit_info hit, float t_min = 0.001, float t_max = 100000000000.0)
{
    vec3 oc = r.orig - s.geom.xyz;
    float a = dot(r.dir, r.dir);
    float half_b = dot(r.dir, oc);
    float c = dot(oc, oc) - s.geom.w * s.geom.w;

    float half_discr = half_b*half_b - a*c;
    if (half_discr < 0) return false;
    
    float sqrtd = sqrt(half_discr);


    float root = (-half_b - sqrtd) / a;
    if (root < t_min || t_max < root) 
        root = (-half_b + sqrtd) / a;
    if (root < t_min || t_max < root)
        return false;
    
    

    hit.t = root;
    hit.p = r.orig + r.dir * hit.t;
    vec3 normal = (hit.p - s.geom.xyz) / s.geom.w;
    hit.front_face = (dot(normalize(r.dir), normal) <= 0);

    hit.outward_normal = normal;
    hit.normal = hit.front_face ? normal : -normal;
    hit.material = s.material;
    hit.albedo = s.albedo;
    hit.roughness = s.roughness;
    hit.refr_index = s.refr_index;

    return true;
}

void material_scatter(hit_info hit, inout Ray scatter_ray)
{
    if(hit.material == LAMBERTIAN_MAT)
        lambertian_scatter(hit, scatter_ray);
    else if(hit.material == REFLECTIVE_MAT)
        reflective_scatter(hit, scatter_ray);
    else if(hit.material == REFRACTIVE_MAT)
        refractive_scatter(hit, scatter_ray);
}

void lambertian_scatter(hit_info hit, out Ray scatter_ray)
{ 
    vec3 scattered_ray_dir = hit.normal + random_on_unit_sphere(hit.p);
    scatter_ray = Ray(hit.p, scattered_ray_dir);
}

void reflective_scatter(hit_info hit, inout Ray scatter_ray)
{ 
    vec3 scattered_ray_dir = reflect(normalize(scatter_ray.dir), hit.normal);
    //scattered_ray_dir += random_on_unit_sphere(hit.p) * hit.roughness * random(hit.p + vec3(11.11111, 5656.9943, 7.23));
    scattered_ray_dir += random(hit.p + vec3(22.222, 3654.3, 66.1)) * random_on_unit_sphere(hit.p) * hit.roughness;

    scatter_ray = Ray(hit.p, scattered_ray_dir);
}

void refractive_scatter(hit_info hit, inout Ray scatter_ray)
{
    float ri_ratio = hit.front_face ? (1.0/hit.refr_index) : (hit.refr_index);
    
    vec3 unit_ray_dir = normalize(scatter_ray.dir);
    float cos_theta = min(dot(-unit_ray_dir, hit.normal), 1.0);
    float sin_theta = sqrt(1.0 - cos_theta*cos_theta);

    bool cannot_refract = ri_ratio * sin_theta > 1.0;
    
    vec3 scattered_ray_dir;
    if(cannot_refract || fresnel_schlick_approximation(cos_theta, ri_ratio) > random(hit.p))
        scattered_ray_dir =  reflect(normalize(scatter_ray.dir), normalize(hit.normal));
    else
        scattered_ray_dir =  refract(normalize(scatter_ray.dir), normalize(hit.normal), ri_ratio);

    scatter_ray = Ray(hit.p, scattered_ray_dir);
}


// Utility Functions
float degrees_to_radians(float degrees)
{
    return degrees * PI / 180.0;
}


vec3 ray_point(Ray r, float t)
{
    return r.orig + r.dir * t; 
}

Ray get_ray(vec2 uv)
{
    const float lens_radius = APERTURE / 2.0;
    const float focus_dist = distance(LOOK_FROM, LOOK_AT);

    const float aspect_raito = imageSize.x / imageSize.y;
    const float viewport_height = 2.0 * tan(degrees_to_radians(VERTICAL_FIELD_OF_VIEW) / 2);
    const float viewport_width = aspect_raito * viewport_height;

    
    vec3 viewDir = normalize(LOOK_FROM - LOOK_AT);
    vec3 rightDir = normalize(cross(VECTOR_UP, viewDir));
    vec3 upDir = cross(viewDir, rightDir);

    
    const vec3 ray_origin = LOOK_FROM;
    const vec3 horizontal = viewport_width * rightDir;
    const vec3 vertical = viewport_height * upDir;  

    vec3 lower_left_corner = ray_origin - horizontal / 2.0 - vertical / 2.0 - viewDir;
    return Ray(ray_origin, lower_left_corner + horizontal * uv.x + vertical * uv.y - ray_origin);
}

bool isInRange(float t, float t_min, float t_max)
{
    return !(t < t_min || t > t_max);
}

float fresnel_schlick_approximation(float cosine, float ri_ratio)
{
    float R0 = (1.0 - ri_ratio) / (1 + ri_ratio);
    R0 = R0 * R0;
    return R0 + (1 - R0)*pow((1- cosine), 5);
}

// A single iteration of Bob Jenkins' One-At-A-Time hashing algorithm.
uint hash( uint x ) {
    x += ( x << 10u );
    x ^= ( x >>  6u );
    x += ( x <<  3u );
    x ^= ( x >> 11u );
    x += ( x << 15u );
    return x;
}

// Compound versions of the hashing algorithm I whipped together.
uint hash( uvec2 v ) { return hash( v.x ^ hash(v.y)                         ); }
uint hash( uvec3 v ) { return hash( v.x ^ hash(v.y) ^ hash(v.z)             ); }
uint hash( uvec4 v ) { return hash( v.x ^ hash(v.y) ^ hash(v.z) ^ hash(v.w) ); }

// Construct a float with half-open range [0:1] using low 23 bits.
// All zeroes yields 0.0, all ones yields the next smallest representable value below 1.0.
float floatConstruct( uint m ) {
    const uint ieeeMantissa = 0x007FFFFFu; // binary32 mantissa bitmask
    const uint ieeeOne      = 0x3F800000u; // 1.0 in IEEE binary32

    m &= ieeeMantissa;                     // Keep only mantissa bits (fractional part)
    m |= ieeeOne;                          // Add fractional part to 1.0

    float  f = uintBitsToFloat( m );       // Range [1:2]
    return f - 1.0;                        // Range [0:1]
}


// Pseudo-random value in half-open range [0:1].
float random( uint x ) { return floatConstruct(hash(x)); }
float random( float x ) { return floatConstruct(hash(floatBitsToUint(x))); }
float random( vec2  v ) { return floatConstruct(hash(floatBitsToUint(v))); }
float random( vec3  v ) { return floatConstruct(hash(floatBitsToUint(v))); }
vec3 random3( vec3  v ) { return vec3(floatConstruct(hash(floatBitsToUint(v.x))),floatConstruct(hash(floatBitsToUint(v.y))),floatConstruct(hash(floatBitsToUint(v.z)))); }
float random( vec4  v ) { return floatConstruct(hash(floatBitsToUint(v))); }

// Random point on sphere with radius 1
vec3 random_on_unit_sphere()
{
    float phi = random(seed++) * PI;
    float theta = random(seed++) * 2 * PI;

    return vec3(sin(theta) * cos(phi), 
                sin(theta) * sin(phi), 
                cos(theta));
}

vec3 random_on_unit_sphere(vec3 vec_seed)
{
    float phi = random(vec_seed + vec3(444.4444, 123.321, 8875.543234)) * 2 * PI;
    float theta = random(vec_seed ) * PI;

    return vec3(sin(theta) * cos(phi), 
                sin(theta) * sin(phi), 
                cos(theta));
}

vec3 random_on_unit_sphere(vec2 vec_seed)
{
    float phi = random(vec_seed) * 2 * PI;
    float theta = random(vec_seed + vec2(444.4444, 123.321)) * PI;

    return vec3(sin(theta) * cos(phi), 
                sin(theta) * sin(phi), 
                cos(theta));
}

vec3 random_in_unit_sphere(vec3 vec_seed)
{
    while (true) 
    {
        vec3 p = random3(vec_seed)*2.0 - 1.0;
        if (length(p) >= 1) continue;
        return p;
        vec_seed += vec3(1.0);
    }
}

vec3 random_in_unit_disk(vec3 vec_seed) 
{
    float ro = random(vec_seed.x);
    float phi = random(vec_seed.y) * 2 * PI;

    vec3 p = vec3(ro * cos(phi), ro * sin(phi), 0);
    return p;
}